apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.health.openscale"
        testApplicationId "com.health.openscale.test"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 24
        versionName "1.7.1"

        javaCompileOptions {
            annotationProcessorOptions { arguments = ["room.schemaLocation":"$projectDir/schemas".toString()] }
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

ext {
    supportLibVersion = '27.1.0'
}

dependencies {
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.android.support:support-v4:${supportLibVersion}"
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"

    // HelloCharts
    implementation 'com.github.lecho:hellocharts-library:1.5.8@aar'

    // Simple CSV
    implementation 'com.j256.simplecsv:simplecsv:2.3'

    // CustomActivityOnCrash
    implementation 'cat.ereza:customactivityoncrash:2.2.0'

    // Room
    implementation 'android.arch.persistence.room:runtime:1.0.0'
    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0'
    androidTestImplementation 'android.arch.persistence.room:testing:1.0.0'

    // Local unit tests
    testImplementation 'junit:junit:4.12'

    // Instrumented unit tests
    androidTestImplementation "com.android.support:support-annotations:${supportLibVersion}"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.1'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}
