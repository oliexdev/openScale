apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.health.openscale"
        testApplicationId "com.health.openscale.test"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 44
        versionName "2.0.3"

        manifestPlaceholders = [
                appIcon: "@drawable/ic_launcher_openscale"
        ]

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        light {
            manifestPlaceholders = [
                    appIcon: "@drawable/ic_launcher_openscale_light"
            ]
            applicationIdSuffix ".light"
            versionNameSuffix "-light"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        pro {
            manifestPlaceholders = [
                    appIcon: "@drawable/ic_launcher_openscale_pro"
            ]
            applicationIdSuffix ".pro"
            versionNameSuffix "-pro"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.1.0-alpha04'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    // MPAndroidChart
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    // Simple CSV
    implementation 'com.j256.simplecsv:simplecsv:2.3'
    // RxAndroidBle
    implementation 'com.polidea.rxandroidble2:rxandroidble:1.8.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.jakewharton.rx2:replaying-share:2.1.0'
    // CustomActivityOnCrash
    implementation 'cat.ereza:customactivityoncrash:2.2.0'
    // Room
    implementation 'androidx.room:room-runtime:2.1.0-alpha04'
    annotationProcessor 'androidx.room:room-compiler:2.1.0-alpha04'
    androidTestImplementation 'androidx.room:room-testing:2.1.0-alpha04'
    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'
    // Local unit tests
    testImplementation 'junit:junit:4.12'
    // Instrumented unit tests
    androidTestImplementation 'androidx.annotation:annotation:1.0.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.1'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}
